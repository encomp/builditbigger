// If you would like more information on the gradle-appengine-plugin please refer to the github page
// https://github.com/GoogleCloudPlatform/gradle-appengine-plugin

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:1.0.2'
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:1+'
    }
}

repositories {
    mavenCentral()
}

def projectId = 'test-iot-arduino'

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'com.google.cloud.tools.endpoints-framework-server'
apply plugin: 'com.google.cloud.tools.appengine'

dependencies {
    implementation 'com.google.endpoints:endpoints-framework:2.0.9'
    compileOnly 'javax.inject:javax.inject:1'
    compileOnly 'javax.servlet:javax.servlet-api:3.1.0'
    compile project(':jokes')
    implementation 'com.google.code.gson:gson:2.8.5'
}

endpointsServer {
    hostname = "${projectId}.appspot.com"
}

appengine {
    deploy {
        version = findProperty("appengine.deploy.version")

        def promoteProp = findProperty("appengine.deploy.promote")
        if (promoteProp != null) {
            promote = new Boolean(promoteProp)
        }
    }
}

task replaceProjectId(type: Copy) {
    from 'src/main/webapp/WEB-INF/'
    include '*.xml'
    into "build/exploded-${archivesBaseName}/WEB-INF"
    expand(endpoints: [project: [id: projectId]])
    filteringCharset = 'UTF-8'
}
assemble.dependsOn replaceProjectId
